Require Import floyd.proofauto.
Import ListNotations.
Require Import sha.sha.
Require Import sha.SHA256.
Local Open Scope logic.

Definition s256state := (list val * (val * (val * (list val * val))))%type.
Definition s256_h (s: s256state) := fst s.
Definition s256_Nl (s: s256state) := fst (snd s).
Definition s256_Nh (s: s256state) := fst (snd (snd s)).
Definition s256_data (s: s256state) := fst (snd (snd (snd s))).
Definition s256_num (s: s256state) := snd (snd (snd (snd s))).

Inductive s256abs :=  (* SHA-256 abstract state *)
 S256abs: forall (hashed: list int)   (* words hashed, so far *)
                         (data: list Z)  (* bytes in the partial block not yet hashed *),
                     s256abs.

Definition s256a_regs (a: s256abs) : list int :=
 match a with S256abs hashed data  => 
          hash_blocks init_registers hashed 
 end.

Definition s256a_len (a: s256abs) : Z := 
  match a with S256abs hashed data => 
    (Zlength hashed * WORD + Zlength data) * 8 
  end%Z.

Definition s256_relate (a: s256abs) (r: s256state) : Prop :=
     match a with S256abs hashed data =>
         s256_h r = map Vint (hash_blocks init_registers hashed) 
       /\ (exists hi, exists lo, s256_Nh r = Vint hi /\ s256_Nl r = Vint lo /\
             (Zlength hashed * WORD + Zlength data)*8 = hilo hi lo)%Z
       /\ s256_data r = map Vint (map Int.repr data)
       /\ (Zlength data < CBLOCKz /\ Forall isbyteZ data)
       /\ (LBLOCKz | Zlength hashed)
       /\ s256_num r = Vint (Int.repr (Zlength data))
     end.

Definition init_s256abs : s256abs := S256abs nil nil.

Definition sha_finish (a: s256abs) : list Z :=
 match a with
 | S256abs hashed data => 
     SHA_256 (intlist_to_Zlist hashed ++ data)
 end.

Definition cVint (f: Z -> int) (i: Z) := Vint (f i).

Definition sha256_length (len: Z)  (c: val) : mpred :=
   EX lo:int, EX hi:int, 
     !! (hilo hi lo = len) &&
     (field_at Tsh t_struct_SHA256state_st [_Nl] (Vint lo) c *
      field_at Tsh t_struct_SHA256state_st [_Nh] (Vint hi) c).

Definition sha256state_ (a: s256abs) (c: val) : mpred :=
   EX r:s256state, 
    !!  s256_relate a r  &&  data_at Tsh t_struct_SHA256state_st r c.

Definition tuints (vl: list int) := ZnthV tuint (map Vint vl).
Definition tuchars (vl: list int) :=  ZnthV tuchar (map Vint vl).

Definition data_block (sh: share) (contents: list Z) :=
  !! Forall isbyteZ contents &&
  array_at tuchar sh (tuchars (map Int.repr contents)) 0 (Zlength contents).

Definition _ptr : ident := 81%positive.
Definition _x : ident := 82%positive.

Definition __builtin_read32_reversed_spec :=
 DECLARE ___builtin_read32_reversed
  WITH p: val, sh: share, contents: Z -> int
  PRE [ _ptr OF tptr tuint ] 
        PROP() LOCAL (`(eq p) (eval_id _ptr))
        SEP (`(array_at tuchar sh (cVint contents) 0 4 p))
  POST [ tuint ] 
     local (`(eq (Vint (big_endian_integer contents))) retval) &&
     `(array_at tuchar sh (cVint contents) 0 4 p).

Definition __builtin_write32_reversed_spec :=
 DECLARE ___builtin_write32_reversed
  WITH p: val, sh: share, contents: Z -> int
  PRE [ _ptr OF tptr tuint, _x OF tuint ] 
        PROP(writable_share sh)
        LOCAL (`(eq p) (eval_id _ptr);
                     `(eq (Vint(big_endian_integer contents))) (eval_id _x))
        SEP (`(memory_block sh (Int.repr 4) p))
  POST [ tvoid ] 
     `(array_at tuchar sh (cVint contents) 0 4 p).

Definition memcpy_spec :=
  DECLARE _memcpy
   WITH sh : share*share, p: val, q: val, n: Z, contents: Z -> int 
   PRE [ 1%positive OF tptr tvoid, 2%positive OF tptr tvoid, 3%positive OF tuint ]
       PROP (writable_share (snd sh); 0 <= n <= Int.max_unsigned)
       LOCAL (`(eq p) (eval_id 1%positive); `(eq q) (eval_id 2%positive);
                    `(eq (Vint (Int.repr n))) (eval_id 3%positive))
       SEP (`(array_at tuchar (fst sh) (cVint contents) 0 n q);
              `(memory_block (snd sh) (Int.repr n) p))
    POST [ tptr tvoid ]
         local (`(eq p) retval) &&
       (`(array_at tuchar (fst sh) (cVint contents) 0 n q) *
        `(array_at tuchar (snd sh) (cVint contents) 0 n p)).

Definition memset_spec :=
  DECLARE _memset
   WITH sh : share, p: val, n: Z, c: int 
   PRE [ 1%positive OF tptr tvoid, 2%positive OF tint, 3%positive OF tuint ]
       PROP (writable_share sh; 0 <= n <= Int.max_unsigned)
       LOCAL (`(eq p) (eval_id 1%positive); `(eq (Vint c)) (eval_id 2%positive);
                    `(eq (Vint (Int.repr n))) (eval_id 3%positive))
       SEP (`(memory_block sh (Int.repr n) p))
    POST [ tptr tvoid ]
         local (`(eq p) retval) &&
       (`(array_at tuchar sh (fun _ => Vint c) 0 n p)).

Definition K_vector : val -> mpred :=
  array_at tuint Tsh (tuints K256) 0 (Zlength K256).

Definition sha256_block_data_order_spec :=
  DECLARE _sha256_block_data_order
    WITH hashed: list int, b: list int, ctx : val, data: val, sh: share, kv : val
   PRE [ _ctx OF tptr t_struct_SHA256state_st, _in OF tptr tvoid ]
         PROP(Zlength b = LBLOCKz; (LBLOCKz | Zlength hashed)) 
         LOCAL (`(eq ctx) (eval_id _ctx); `(eq data) (eval_id _in);
                     `(eq kv) (eval_var _K256 (tarray tuint CBLOCKz)))
         SEP (`(array_at tuint Tsh  (tuints (hash_blocks init_registers hashed)) 0 8 ctx);
                `(data_block sh (intlist_to_Zlist b) data);
                 `(K_vector kv))
   POST [ tvoid ]
          (`(array_at tuint Tsh  (tuints (hash_blocks init_registers (hashed++b))) 0 8 ctx) *
          `(data_block sh (intlist_to_Zlist b) data) *
          `(K_vector kv)).
 
Definition SHA256_addlength_spec :=
 DECLARE _SHA256_addlength
 WITH len : Z, c: val, n: Z
 PRE [ _c OF tptr t_struct_SHA256state_st , _len OF tuint ]
   PROP ( 0 <= n+len*8 < two_p 64; 0 <= len <= Int.max_unsigned) 
   LOCAL (`(eq (Vint (Int.repr len))) (eval_id _len); 
               `(eq c) (eval_id _c))
   SEP (`(sha256_length n c))
 POST [ tvoid ]
   `(sha256_length (n+len*8) c).

Definition SHA256_Init_spec :=
  DECLARE _SHA256_Init
   WITH c : val 
   PRE [ _c OF tptr t_struct_SHA256state_st ]
         PROP () LOCAL (`(eq c) (eval_id _c))
         SEP(`(data_at_ Tsh t_struct_SHA256state_st c))
  POST [ tvoid ] 
          (`(sha256state_ init_s256abs c)).

Inductive update_abs: list Z -> s256abs -> s256abs -> Prop :=
 Update_abs:
   (forall msg hashed blocks oldfrag newfrag,
        Zlength oldfrag < CBLOCKz ->
        Zlength newfrag < CBLOCKz ->
       (LBLOCKz | Zlength hashed) ->
       (LBLOCKz | Zlength blocks) -> 
       oldfrag++msg = intlist_to_Zlist blocks ++ newfrag ->
   update_abs msg (S256abs hashed oldfrag) 
                              (S256abs (hashed++blocks) newfrag)).

Definition SHA256_Update_spec :=
  DECLARE _SHA256_Update
   WITH a: s256abs, data: list Z, c : val, d: val, sh: share, len : Z, kv : val
   PRE [ _c OF tptr t_struct_SHA256state_st, _data_ OF tptr tvoid, _len OF tuint ]
         PROP (len <= Zlength data; 0 <= len <= Int.max_unsigned;
                   (s256a_len a + len * 8 < two_p 64)%Z)
         LOCAL (`(eq c) (eval_id _c); `(eq d) (eval_id _data_); 
                                  `(eq (Vint (Int.repr len))) (eval_id _len);
                     `(eq kv) (eval_var _K256 (tarray tuint CBLOCKz)))
         SEP(`(K_vector kv);
               `(sha256state_ a c); `(data_block sh data d))
  POST [ tvoid ] 
         EX a':_, 
          PROP (update_abs (firstn (Z.to_nat len) data) a a')
          LOCAL ()
          SEP(`(K_vector kv);
                `(sha256state_ a' c); `(data_block sh data d)).

Definition SHA256_Final_spec :=
  DECLARE _SHA256_Final
   WITH a: s256abs, md: val, c : val,  shmd: share, sh: share, kv : val
   PRE [ _md OF tptr tuchar, _c OF tptr t_struct_SHA256state_st ]
         PROP (writable_share shmd) 
         LOCAL (`(eq md) (eval_id _md); `(eq c) (eval_id _c);
                     `(eq kv) (eval_var _K256 (tarray tuint CBLOCKz)))
         SEP(`(K_vector kv);
               `(sha256state_ a c);
               `(memory_block shmd (Int.repr 32) md))
  POST [ tvoid ] 
         PROP () LOCAL ()
         SEP(`(K_vector kv);
               `(data_at_ Tsh t_struct_SHA256state_st c);
               `(data_block shmd (sha_finish a) md)).

Definition SHA256_spec :=
  DECLARE _SHA256
   WITH d: val, len: Z, dsh: share, msh: share, data: list Z, md: val, kv : val
   PRE [ _d OF tptr tuchar, _n OF tuint, _md OF tptr tuchar ]
         PROP (writable_share msh; Zlength data * 8 < two_p 64; Zlength data <= Int.max_unsigned) 
         LOCAL (`(eq d) (eval_id _d);
                     `(eq (Vint (Int.repr (Zlength data)))) (eval_id _n);
                     `(eq md) (eval_id _md);
                     `(eq kv) (eval_var _K256 (tarray tuint CBLOCKz)))
         SEP(`(K_vector kv);
               `(data_block dsh data d); `(memory_block msh (Int.repr 32) md))
  POST [ tvoid ] 
         SEP(`(K_vector kv);
               `(data_block dsh data d); `(data_block msh (SHA_256 data) md)).

Definition Vprog : varspecs := (_K256, tarray tuint 64)::nil.

Definition Gprog : funspecs := 
  __builtin_read32_reversed_spec::
  __builtin_write32_reversed_spec::
  memcpy_spec:: memset_spec::
  sha256_block_data_order_spec:: SHA256_Init_spec::
  SHA256_addlength_spec::
  SHA256_Update_spec:: SHA256_Final_spec::
  SHA256_spec:: nil.

Fixpoint do_builtins (n: nat) (defs : list (ident * globdef fundef type)) : funspecs :=
 match n, defs with
  | S n', (id, Gfun (External (EF_builtin _ sig) argtys resty cc_default))::defs' => 
     (id, mk_funspec (iota_formals 1%positive argtys, resty) unit FF FF) 
      :: do_builtins n' defs'
  | _, _ => nil
 end.

Definition Gtot := do_builtins 3 (prog_defs prog) ++ Gprog.






